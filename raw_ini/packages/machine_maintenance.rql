
// all the data sources
maint := read("s3://raw-tutorial/ipython-demos/predictive-maintenance/maint.parquet", cache := interval "10 hours");
machines := read("s3://raw-tutorial/ipython-demos/predictive-maintenance/machines.parquet", cache := interval "10 hours");
telemetry := read("s3://raw-tutorial/ipython-demos/predictive-maintenance/telemetry.parquet", cache := interval "10 hours");
failures := read("s3://raw-tutorial/ipython-demos/predictive-maintenance/failures.parquet", cache := interval "10 hours");
errors := read("s3://raw-tutorial/ipython-demos/predictive-maintenance/errors.parquet", cache := interval "10 hours");
machine_data := read("s3://raw-tutorial/ipython-demos/predictive-maintenance/machine_data.parquet", cache := interval "10 hours");

// creates a records to be used as features, one per day, with nested data with:
// measurements over the last days and failures over the next days.  
//      measure_period: period to collected measures in the past days.
//      predict_period: period to try to predict failures over the next days
//      start: start date to collect features from
//      end: end date to collect features from
create_dataset(measure_period: interval, predict_period: interval, start: date, end: date) := {
    in_measure_period(t: timestamp, d: date) := t <= d and t > d - measure_period;
    in_predict_period(t: timestamp, d: date) := t >= d and t < d + predict_period;

    days := select distinct cast(t.datetime as date) date, m.machineID, m.model, m.age
        from telemetry t, machines m
        where t.machineID = m.machineID and t.datetime > start and t.datetime < end;

    select date, machineID, model, age,
            select p.datetime, p.volt, p.rotate, p.vibration from telemetry p
                where p.machineID = machineID and in_measure_period(p.datetime, date)
                as measurements,
            select f.datetime, f.failure from failures f
                where f.machineID = machineID and in_predict_period(f.datetime, date)
                as failures,
            select e.error, e.datetime  from errors e
                where e.machineID=machineID and in_measure_period(e.datetime, date)
                as errors,
            select max(c1.datetime) from maint c1
                where c1.machineID = machineID and c1.comp = "comp1" and c1.datetime <= date
                as lastC1,
            select max(c2.datetime) from maint c2
                where c2.machineID = machineID and c2.comp = "comp2" and c2.datetime <= date
                as lastC2,
            select max(c3.datetime) from maint c3
                where c3.machineID = machineID and c3.comp = "comp3" and c3.datetime <= date
                as lastC3,
            select max(c4.datetime) from maint c4
                where c4.machineID = machineID and c4.comp = "comp4" and c4.datetime <= date
                as lastC4,
            select max(f2.datetime) from failures f2
                where f2.machineID = machineID and f2.datetime <= date
                as lastFailure
        from days

};

//Creates a normalized dataset, (transforms everything to numbers) using the create_dataset function above
dataset_normalized(measure_period: interval, predict_period: interval, start: date, end: date) := {
   to_number(s: string, i: int, l: int := 1) :=  cast(substr(s, i, l) as int);

   select date,
        machineID,
        to_number(model, 6) as model,
        age,
        select avg(m.volt) from measurements m as volt,
        select avg(m.rotate) from measurements m as rotate,
        select avg(m.vibration) from measurements m as vibrate,
        interval_to_millis(date - lastC1 )/ 3600000 as lastC1,
        interval_to_millis(date - lastC2)/ 3600000 as lastC2,
        interval_to_millis(date - lastC3)/ 3600000 as lastC3,
        interval_to_millis(date - lastC4)/ 3600000 as lastC4,
        ccount(errors) as errors,
        isnull(interval_to_millis(date - lastFailure)/ 3600000, -1) as lastFailure,
        if (ccount(failures) > 0)
            then (select to_number(f.failure, 5) from failures f)
            else [0] as failures
    from create_dataset(measure_period, predict_period, start, end)
    where ccount(measurements) > 0
};


//Creates a features, using the dataset_normalized function above
features(measure_period: interval, predict_period: interval, start: date, end: date) := {
       select  [model,
                        age,
                        volt,
                        rotate,
                        vibrate,
                        lastC1,
                        lastC2,
                        lastC3,
                        lastC4,
                        errors,
                        lastFailure] as features,
            failure
        from dataset_normalized(measure_period, predict_period, start, end), failures failure
};
